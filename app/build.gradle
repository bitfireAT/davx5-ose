/*
 * Copyright (c) Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId "at.bitfire.davdroid"

        versionCode 303050000
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"

        minSdkVersion 21        // Android 5
        targetSdkVersion 30     // Android 11

        buildConfigField "String", "userAgent", "\"DAVx5\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        // enable because ical4android requires desugaring
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures.dataBinding = true

    flavorDimensions "distribution"
    productFlavors {
        standard {
            versionName "3.3.5-ose"
        }
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-release.pro'

            shrinkResources true
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'                  // we don't need Google indexing, thanks
        disable 'ImpliedQuantity', 'MissingQuantity'        // quantities from Transifex may vary
        disable 'MissingTranslation', 'ExtraTranslation'    // translations from Transifex are not always up to date
        disable 'RtlEnabled', 'RtlHardcoded'                // RTL not supported yet
        disable 'Typos'
    }

    dokka.configuration {
        sourceLink {
            url = "https://gitlab.com/bitfireAT/davx5-ose/tree/master-ose/"
            lineSuffix = "#L"
        }
        jdkVersion = 8

        externalDocumentationLink {
            url = new URL("https://bitfireat.gitlab.io/cert4android/dokka/cert4android/")
            packageListUrl = new URL("https://bitfireat.gitlab.io/cert4android/dokka/cert4android/package-list")
        }
        externalDocumentationLink {
            url = new URL("https://bitfireat.gitlab.io/dav4jvm/dokka/dav4jvm/")
            packageListUrl = new URL("https://bitfireat.gitlab.io/dav4jvm/dokka/dav4jvm/package-list")
        }
        externalDocumentationLink {
            url = new URL("https://bitfireat.gitlab.io/ical4android/dokka/ical4android/")
            packageListUrl = new URL("https://bitfireat.gitlab.io/ical4android/dokka/ical4android/package-list")
        }
        externalDocumentationLink {
            url = new URL("https://bitfireat.gitlab.io/vcard4android/dokka/vcard4android/")
            packageListUrl = new URL("https://bitfireat.gitlab.io/vcard4android/dokka/vcard4android/package-list")
        }
    }
}

dependencies {
    implementation project(':cert4android')
    implementation project(':ical4android')
    implementation project(':vcard4android')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.paging:paging-runtime-ktx:2.1.2'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'

    def room_version = '2.2.5'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation "com.github.AppIntro:AppIntro:${versions.appIntro}"
    implementation "com.gitlab.bitfireAT:dav4jvm:${versions.dav4jvm}"
    implementation "com.mikepenz:aboutlibraries:${versions.aboutLibraries}"
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.squareup.okhttp3:okhttp-brotli:${versions.okhttp}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"
    implementation 'commons-io:commons-io:2.6'
    //noinspection GradleDependency - dnsjava 3+ needs Java 8/Android 7
    implementation 'dnsjava:dnsjava:2.1.9'
    //noinspection GradleDependency
    implementation "org.apache.commons:commons-collections4:${versions.commonsCollections}"
    //noinspection GradleDependency
    implementation "org.apache.commons:commons-lang3:${versions.commonsLang}"
    //noinspection GradleDependency
    implementation "org.apache.commons:commons-text:${versions.commonsText}"

    // for tests
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"

    testImplementation 'junit:junit:4.13'
    testImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"
}
