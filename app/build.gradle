/*
 * Copyright (c) 2013 â€“ 2016 Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "at.bitfire.davdroid"
        minSdkVersion 14
        targetSdkVersion 22

        versionCode 91
        versionName "1.0_1"

        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'      // we don't need Google indexing, thanks
        disable 'GradleDependency'
        disable 'GradleDynamicVersion'
        disable 'IconColors'
        disable 'IconLauncherShape'
        disable 'IconMissingDensityFolder'
        disable 'MissingTranslation'
        disable 'OldTargetApi'      // Android 6 permission model not implemented yet
        disable 'Recycle'           // doesn't understand Lombok's @Cleanup
        disable 'RtlEnabled'
        disable 'RtlHardcoded'
        disable 'Typos'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

configurations.all {
    exclude module: 'commons-logging'       // undocumented part of Android
}

dependencies {
    provided 'org.projectlombok:lombok:1.16.6'

    compile project(':dav4android')
    compile project(':ical4android')
    compile project(':vcard4android')

    compile 'com.android.support:appcompat-v7:23.+'
    compile 'com.android.support:cardview-v7:23.+'
    compile 'com.android.support:design:23.+'
    compile 'com.android.support:preference-v7:23.+'

    compile 'com.github.yukuku:ambilwarna:2.0.1'
    compile project(':MemorizingTrustManager')

    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'org.apache.commons:commons-lang3:3.4'
}
