/*
 * Copyright (c) Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "at.bitfire.davdroid"
        resValue "string", "packageID", applicationId

        versionCode 181
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"

        minSdkVersion 16
        targetSdkVersion 25

        buildConfigField "boolean", "customCerts", "false"
    }

    productFlavors {
        standard {
            versionName "1.9-beta3"
            buildConfigField "boolean", "customCerts", "true"
        }
        managed {
            applicationId "com.davdroid.managed"
            resValue "string", "packageID", applicationId
            minSdkVersion 21

            versionName "1.9-beta3-mgd"
        }

        gplay {
            versionName "1.9-beta3-gplay"
            buildConfigField "boolean", "customCerts", "true"
        }
        icloud {
            applicationId "at.bitfire.cloudsync"
            resValue "string", "packageID", applicationId

            versionName "1.9-beta3-cloud"
        }
        soldupe {
            applicationId "com.soldupe.cloudsync"
            resValue "string", "packageID", applicationId
            minSdkVersion 21

            versionName "1.9-beta3-soldupe"
        }
    }

    sourceSets {
        standard.java.srcDirs = [ "src/davdroid/java" ]
        standard.res.srcDirs = [ "src/davdroid/res" ]
        gplay.java.srcDirs = [ "src/gplay/java", "src/davdroid/java" ]
        gplay.res.srcDirs = [ "src/gplay/res", "src/davdroid/res" ]
    }

    signingConfigs {
        bitfire {
            storeFile file("${System.env.HOME}/Entwicklung/GooglePlay/bitfire.jks")
            storePassword '***REMOVED***'
            keyAlias 'bitfire'
            keyPassword '***REMOVED***'
        }
        soldupe {
            storeFile file("${System.env.HOME}/Entwicklung/GooglePlay/soldupe.jks")
            storePassword 'hei8eePh'
            keyAlias 'soldupe'
            keyPassword 'ocaip6oZ'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            signingConfig signingConfigs.bitfire
            productFlavors.soldupe.signingConfig signingConfigs.soldupe
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'      // we don't need Google indexing, thanks
        disable 'GradleDependency'
        disable 'IconColors'
        disable 'IconLauncherShape'
        disable 'IconMissingDensityFolder'
        disable 'ImpliedQuantity', 'MissingQuantity'
        disable 'MissingTranslation', 'ExtraTranslation'	// translations from Transifex are not always up to date
        disable 'Recycle'           // doesn't understand Lombok's @Cleanup
        disable 'RtlEnabled'
        disable 'RtlHardcoded'
        disable 'Typos'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    compile project(':cert4android')
    compile project(':dav4android')
    compile project(':ical4android')
    compile project(':vcard4android')

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:cardview-v7:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'com.android.support:preference-v14:26.0.1'

    compile 'com.github.yukuku:ambilwarna:2.0.1'

    compile 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    compile 'commons-io:commons-io:2.5'
    compile 'dnsjava:dnsjava:2.1.8'
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'org.apache.commons:commons-collections4:4.1'

    // for tests
    //noinspection GradleDynamicVersion
    androidTestCompile('com.android.support.test:runner:+') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    //noinspection GradleDynamicVersion
    androidTestCompile('com.android.support.test:rules:+') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.9.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.9.0'
}
